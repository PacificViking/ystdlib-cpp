include(CMakeFindDependencyMacro)

@PACKAGE_INIT@

# If no COMPONENTS are specified, we include all available libraries.
if(NOT ystdlib_FIND_COMPONENTS)
    set(ystdlib_LIBRARIES_LIST "@ystdlib_LIBRARIES@")
    foreach(LIB IN LISTS ystdlib_LIBRARIES_LIST)
        if(NOT TARGET ystdlib::${LIB})
            include("${CMAKE_CURRENT_LIST_DIR}/libs/${LIB}-config.cmake")
        endif()
    endforeach()
else()
    foreach(LIB IN LISTS ystdlib_FIND_COMPONENTS)
        if(NOT TARGET "ystdlib::${LIB}")
            if(ystdlib_FIND_REQUIRED_${LIB})
                include("${CMAKE_CURRENT_LIST_DIR}/libs/${LIB}-config.cmake")
            else()
                include("${CMAKE_CURRENT_LIST_DIR}/libs/${LIB}-config.cmake" OPTIONAL)
            endif()
        endif()
    endforeach()
endif()

check_required_components(ystdlib)
